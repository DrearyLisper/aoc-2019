#lang racket

(provide execute execute-with-input state-struct-program state-struct-output)

(require threading)
(require racket/trace)

(struct opcode-struct (f args len) #:transparent)
(struct state-struct (program ip halted input output) #:mutable #:transparent)

(define (sum state opcode)
  (match-let* ((program (state-struct-program state))
               (ip (state-struct-ip state))
               ((list arg-a arg-b arg-c) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1)))
               (b (vector-ref program (+ ip 2)))
               (o (vector-ref program (+ ip 3))))
    (vector-set! program
                 (arg-c program o)
                 (+ (arg-a program a)
                    (arg-b program b)))
    (set-state-struct-ip! state (+ ip (opcode-struct-len opcode)))
    state))

(define (prod state opcode)
  (match-let* ((program (state-struct-program state))
               (ip (state-struct-ip state))
               ((list arg-a arg-b arg-c) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1)))
               (b (vector-ref program (+ ip 2)))
               (o (vector-ref program (+ ip 3))))
    (vector-set! program
                 (arg-c program o)
                 (* (arg-a program a)
                    (arg-b program b)))
    (set-state-struct-ip! state (+ ip (opcode-struct-len opcode)))
    state))


(define (halt state opcode)
  (set-state-struct-halted! state #t)
  state)

(define (input state opcode)
  (match-let* ((program (state-struct-program state))
               (input (state-struct-input state))
               (ip (state-struct-ip state))
               ((list arg-a) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1))))
    (vector-set! program (arg-a program a) (first input))
    (set-state-struct-input! state (rest input))
    (set-state-struct-ip! state (+ ip (opcode-struct-len opcode)))
    state))

(define (output state opcode)
  (match-let* ((program (state-struct-program state))
               (output (state-struct-output state))
               (ip (state-struct-ip state))
               ((list arg-a) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1))))
    (set-state-struct-output! state (cons (arg-a program a) output))
    (set-state-struct-ip! state (+ ip (opcode-struct-len opcode)))
    state))

(define (jump-if-true state opcode)
  (match-let* ((program (state-struct-program state))
               (output (state-struct-output state))
               (ip (state-struct-ip state))
               ((list arg-a arg-b) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1)))
               (b (vector-ref program (+ ip 2))))
    (if (not (equal? (arg-a program a) 0))
        (set-state-struct-ip! state (arg-b program b))
        (set-state-struct-ip! state (+ ip (opcode-struct-len opcode))))
    state))

(define (jump-if-false state opcode)
  (match-let* ((program (state-struct-program state))
               (output (state-struct-output state))
               (ip (state-struct-ip state))
               ((list arg-a arg-b) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1)))
               (b (vector-ref program (+ ip 2))))
    (if (equal? (arg-a program a) 0)
        (set-state-struct-ip! state (arg-b program b))
        (set-state-struct-ip! state (+ ip (opcode-struct-len opcode))))
    state))

(define (less-than state opcode)
  (match-let* ((program (state-struct-program state))
               (output (state-struct-output state))
               (ip (state-struct-ip state))
               ((list arg-a arg-b arg-c) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1)))
               (b (vector-ref program (+ ip 2)))
               (c (vector-ref program (+ ip 3))))

    (if (< (arg-a program a) (arg-b program b))
        (vector-set! program (arg-c program c) 1)
        (vector-set! program (arg-c program c) 0))
    (set-state-struct-ip! state (+ ip (opcode-struct-len opcode)))
    state))

(define (equals state opcode)
  (match-let* ((program (state-struct-program state))
               (output (state-struct-output state))
               (ip (state-struct-ip state))
               ((list arg-a arg-b arg-c) (opcode-struct-args opcode))
               (a (vector-ref program (+ ip 1)))
               (b (vector-ref program (+ ip 2)))
               (c (vector-ref program (+ ip 3))))

    (if (equal? (arg-a program a) (arg-b program b))
        (vector-set! program (arg-c program c) 1)
        (vector-set! program (arg-c program c) 0))
    (set-state-struct-ip! state (+ ip (opcode-struct-len opcode)))
    state))

(define (parse-args opcode type)
  (define (select-mode mode)
    (match mode
      (0 (lambda (program addr) (vector-ref program addr)))
      (1 (lambda (program addr) addr))))

  (define (mode-for-position position)
    (match position
      (0 (remainder (quotient opcode 100) 10))
      (1 (remainder (quotient opcode 1000) 10))
      (2 (remainder (quotient opcode 10000) 10))))

  (match type
    ('sum (map select-mode (list (mode-for-position 0) (mode-for-position 1) 1)))
    ('prod (map select-mode (list (mode-for-position 0) (mode-for-position 1) 1)))
    ('halt (list))
    ('input (map select-mode (list 1)))
    ('output (map select-mode (list (mode-for-position 0))))
    ('jump-if-true (map select-mode (list (mode-for-position 0) (mode-for-position 1))))
    ('jump-if-false (map select-mode (list (mode-for-position 0) (mode-for-position 1))))
    ('less-than (map select-mode (list (mode-for-position 0) (mode-for-position 1) 1)))
    ('equals (map select-mode (list (mode-for-position 0) (mode-for-position 1) 1)))))

(define (parse-opcode opcode)
  (match (remainder opcode 100)
    (1 (opcode-struct sum (parse-args opcode 'sum) 4))
    (2 (opcode-struct prod (parse-args opcode 'prod) 4))
    (3 (opcode-struct input (parse-args opcode 'input) 2))
    (4 (opcode-struct output (parse-args opcode 'output) 2))
    (5 (opcode-struct jump-if-true (parse-args opcode 'jump-if-true) 3))
    (6 (opcode-struct jump-if-false (parse-args opcode 'jump-if-false) 3))
    (7 (opcode-struct less-than (parse-args opcode 'less-than) 4))
    (8 (opcode-struct equals (parse-args opcode 'equals) 4))
    (99 (opcode-struct halt (parse-args opcode 'halt) 1))))

(define (execute program)
  (define (execute-opcode state)
    (let* ((opcode-raw (vector-ref (state-struct-program state) (state-struct-ip state)))
           (opcode (parse-opcode opcode-raw)))
      ((opcode-struct-f opcode) state opcode)))

  (define (execute-impl state)
    (cond ((state-struct-halted state) state)
          (#t (execute-impl (execute-opcode state)))))

  (let* ((state (state-struct program 0 #f (list) (list))))
    (execute-impl state)))

(define (execute-with-input program input)
  (define (execute-opcode state)
    (let* ((opcode-raw (vector-ref (state-struct-program state) (state-struct-ip state)))
           (opcode (parse-opcode opcode-raw)))
      ((opcode-struct-f opcode) state opcode)))

  (define (execute-impl state)
    (cond ((state-struct-halted state) state)
          (#t (execute-impl (execute-opcode state)))))

  (let* ((state (state-struct program 0 #f input (list))))
    (execute-impl state)))
